// Manifest version information!
def versionMajor = 1
def versionMinor = 2
def versionPatch = 3
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

apply plugin: 'com.android.application'

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm:ss'Z'", TimeZone.getTimeZone("UTC"))
// whether the build environment is Travis CI
def isTravis = "true".equals(System.getenv("TRAVIS"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))
// get the keystore properties file
def propsFile = rootProject.file('keystore.properties')

// method to generate version code depend on min sdk version
def getVersionCode = {minSdkVersion ->
    return 2000000000 + versionMajor * 10000000 + versionMinor * 100000 + versionPatch * 1000 + versionBuild * 100 + minSdkVersion
}

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    dataBinding {
        enabled = true
    }

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !isTravis
    }

    signingConfigs {
        release {
            if (propsFile.exists()) {
                // properties file exists.
                // read signing key from properties file
                def props = new Properties()
                props.load(new FileInputStream(propsFile))
                storeFile = rootProject.file(props['storeFile'])
                storePassword = props['storePassword']
                keyAlias = props['keyAlias']
                keyPassword = props['keyPassword']
            } else if (System.console() != null) {
                // console is available, ask user to input key information manually
                storeFile rootProject.file(System.console().readLine("\nKeystore file path: "))
                storePassword System.console().readLine("\nKeystore password: ")
                keyAlias System.console().readLine("\nKey alias: ")
                keyPassword System.console().readLine("\nKey password: ")
            } else {
                // missing keystore information, and no console is available. Will cause error
                // during build
                storeFile file("missing.keystore")
                storePassword ""
                keyAlias ""
                keyPassword ""
            }
        }
    }

    defaultConfig {
        applicationId "com.app.missednotificationsreminder"
        targetSdkVersion 26
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""

        signingConfig signingConfigs.release

        testInstrumentationRunner "com.app.missednotificationsreminder.ApplicationTestRunner"
    }
    buildTypes {
        debug {
            // the debug application will have separate suffix in package so both debug and release
            // can be installed at the same time
            applicationIdSuffix '.debug'
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            // generate more meaningful app file name in format
            // <Project_Name>-production-v<Version_Name>.apk.
            // Example: MissingNOtificationsReminder-release-v1.0.0.apk
                applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    def newName = outputFileName
                    newName = newName.replace("app", rootProject.name)
                    newName = newName.replace("release", "release-v" + variant.mergedFlavor.versionName)
                    outputFileName = newName
                }
            }
        }
    }
    flavorDimensions "version"
    productFlavors {
        v18 {
            minSdkVersion 18
            versionCode getVersionCode(18)
            dimension "version"
        }
        v14 {
            minSdkVersion 14
            versionCode getVersionCode(14)
            dimension "version"
        }
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
        fatal 'UnusedResources'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

configurations.all {
    resolutionStrategy {
        force 'com.android.support:support-annotations:28.0.0'
    }
}

ext {
    androidSupportLibraryVersion = "28.0.0"
    leakCanaryVersion = "1.5.1"
    timberVersion = '4.7.1'
}

dependencies {
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'

    implementation "com.jakewharton.timber:timber:${timberVersion}"

    debugImplementation 'com.jakewharton:butterknife:10.1.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.1.0'
    debugImplementation 'com.jakewharton.madge:madge:1.1.4'
    debugImplementation 'com.jakewharton.scalpel:scalpel:1.1.2'
    debugImplementation 'com.jakewharton.threetenabp:threetenabp:1.2.1'
    debugImplementation 'com.squareup.okio:okio:2.3.0'
    debugImplementation 'com.mattprecious.telescope:telescope:1.5.0@aar'

    implementation 'com.squareup.dagger:dagger:1.2.5'
    annotationProcessor 'com.squareup.dagger:dagger-compiler:1.2.5'
    testAnnotationProcessor 'com.squareup.dagger:dagger-compiler:1.2.5'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.3'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.3'

    implementation 'com.squareup.picasso:picasso:2.71828'

    implementation 'io.reactivex:rxjava:1.3.8'
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'com.tbruyelle.rxpermissions:rxpermissions:0.7.0@aar'
    implementation 'com.jakewharton.rxbinding:rxbinding:1.0.1'

    implementation 'com.f2prateek.rx.preferences:rx-preferences:1.0.2'

    implementation 'com.appyvet:materialrangebar:1.3'
    implementation 'com.wdullaer:materialdatetimepicker:3.6.4'

    implementation 'com.evernote:android-job:1.2.6'

    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    // Espresso-contrib for DatePicker, RecyclerView, Drawer actions, Accessibility checks, CountingIdlingResource
    androidTestImplementation('androidx.test.espresso:espresso-contrib:3.2.0') {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:3.0.0'
    testImplementation 'com.google.truth:truth:1.0'
}
